%{
#include <stdio.h>
#include "util.h"
#include "symbol.h" 
#include "errormsg.h"
#include "absyn.h"

int yylex(void); /* function prototype */

A_exp absyn_root;

void yyerror(char *s)
{
 EM_error(EM_tokPos, "%s", s);
}
%}


%union {
    int pos;
    int ival;
    string sval;

    A_exp exp;
    A_exp term;
    A_var lvalue;
    A_ty        ty;

    A_dec       vardec;
    A_dec       typedec;
    A_dec       funcdec;

    A_decList  decs;
    A_expList expseq;
    A_expList  args;

    A_efieldList recfields;
    A_fieldList tyfields;
}


%token <sval> ID
%token <ival> INT
%token <sval> STRING

%type <exp> exp
%type <term> term
%type <lvalue> lvalue
%type <ty> ty

%type <vardec> vardec
%type <typedec> typedec
%type <funcdec> funcdec

%type <decs> decs
%type <expseq> expseq
%type <expseq> expseq1
%type <args> args
%type <args> args1
%type <tyfields> tyfields
%type <tyfields> tyfields1

%type <recfields> recfields
%type <recfields> recfields1

%token 
  COMMA COLON SEMICOLON LPAREN RPAREN LBRACK RBRACK 
  LBRACE RBRACE DOT 
  PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE
  AND OR ASSIGN
  ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF 
  BREAK NIL
  FUNCTION VAR TYPE 

%nonassoc OF
%nonassoc IF THEN WHILE DO FOR TO
%left ELSE
%nonassoc ASSIGN 
%left OR AND
%nonassoc EQ NEQ GT LT GE LE
%left PLUS MINUS 
%left TIMES DIVIDE

%start program

%%

program:	exp						        {absyn_root=$1;}

term:      INT                              {$$ = A_IntExp(EM_tokPos, $1); }
        |  STRING                           {$$ = A_StringExp(EM_tokPos, $1);}
        |  lvalue                           {$$ = A_VarExp(EM_tokPos, $1);}
        |  LPAREN expseq RPAREN             {$$ = A_SeqExp(EM_tokPos, $2);}
        |  MINUS exp                        {$$ = A_OpExp(EM_tokPos, A_minusOp, A_IntExp(EM_tokPos, 0),  $2);}


exp:       term                             {$$ = $1; }
        |  NIL                              {$$ = A_NilExp(EM_tokPos); }
        |  IF exp THEN exp                  {$$ = A_IfExp(EM_tokPos, $2, $4, A_NilExp(EM_tokPos));} 
        |  IF exp THEN exp ELSE exp         {$$ = A_IfExp(EM_tokPos, $2, $4, $6);}
        |  WHILE exp DO exp                 {$$ = A_WhileExp(EM_tokPos, $2, $4);}
        |  FOR ID ASSIGN exp TO exp DO exp  {$$ = A_ForExp(EM_tokPos, S_Symbol($2), $4, $6, $8);}         
        |  BREAK                            {$$ = A_BreakExp(EM_tokPos);}
        |  LET decs IN expseq END           {$$ = A_LetExp(EM_tokPos, $2, A_SeqExp(EM_tokPos, $4));}     
        |  ID LPAREN args RPAREN            {$$ = A_CallExp(EM_tokPos, S_Symbol($1), $3);}
        |  ID LBRACE recfields RBRACE       {$$ = A_RecordExp(EM_tokPos, S_Symbol($1), $3);}
        |  ID LBRACK exp RBRACK OF exp      {$$ = A_ArrayExp(EM_tokPos, S_Symbol($1), $3, $6);}
        |  exp TIMES exp                    {$$ = A_OpExp(EM_tokPos, A_timesOp, $1, $3);}
        |  exp DIVIDE exp                   {$$ = A_OpExp(EM_tokPos, A_divideOp, $1, $3);}
        |  exp PLUS exp                     {$$ = A_OpExp(EM_tokPos, A_plusOp, $1, $3);} 
        |  exp MINUS exp                    {$$ = A_OpExp(EM_tokPos, A_minusOp, $1, $3);}
        |  exp EQ exp                       {$$ = A_OpExp(EM_tokPos, A_eqOp, $1, $3);}
        |  exp NEQ exp                      {$$ = A_OpExp(EM_tokPos, A_neqOp, $1, $3);}
        |  exp LT exp                       {$$ = A_OpExp(EM_tokPos, A_ltOp, $1, $3);}
        |  exp LE exp                       {$$ = A_OpExp(EM_tokPos, A_leOp, $1, $3);}
        |  exp GT exp                       {$$ = A_OpExp(EM_tokPos, A_gtOp, $1, $3);}
        |  exp GE exp                       {$$ = A_OpExp(EM_tokPos, A_geOp, $1, $3);}
        |  exp OR exp                       {$$ = A_IfExp(EM_tokPos, $1, A_IntExp(EM_tokPos, 1), $3);}
        |  exp AND exp                      {$$ = A_IfExp(EM_tokPos, $1, $3, A_IntExp(EM_tokPos, 0));}
        |  lvalue ASSIGN exp                {$$ = A_AssignExp(EM_tokPos, $1, $3);}

expseq: /*empty*/                           {$$ = NULL;}    
        | expseq1                           {$$ = $1;}

expseq1: exp                                {$$ = A_ExpList($1, NULL);}
        |  exp SEMICOLON expseq1            {$$ = A_ExpList($1, $3);}

args:   /* empty */                         {$$ = NULL;}    
        |  args1                            {$$ = $1;}

args1:  exp                                 {$$ = A_ExpList($1, NULL);}
        |  exp COMMA args1             		{$$ = A_ExpList($1, $3);}
        
recfields: /* empty */                      {$$ = NULL;}    
        |  recfields1                       {$$ = $1;}

recfields1: ID EQ exp                    	{$$ = A_EfieldList(A_Efield(S_Symbol($1), $3), NULL);}
        |  ID EQ exp COMMA recfields1  	    {$$ = A_EfieldList(A_Efield(S_Symbol($1), $3), $5);}

lvalue: ID                             		{$$ = A_SimpleVar(EM_tokPos, S_Symbol($1));}
        | lvalue DOT ID                		{$$ = A_FieldVar(EM_tokPos, $1, S_Symbol($3));}
        | lvalue LBRACK exp RBRACK    		{$$ = A_SubscriptVar(EM_tokPos, $1, $3);}
        | ID LBRACK exp RBRACK         		{$$ = A_SubscriptVar(EM_tokPos, A_SimpleVar(EM_tokPos, S_Symbol($1)), $3);}

ty:     ID                             		{$$ = A_NameTy(EM_tokPos, S_Symbol($1));}
        |  LBRACE tyfields RBRACE      		{$$ = A_RecordTy(EM_tokPos, $2);}
        |  ARRAY OF ID                 		{$$ = A_ArrayTy(EM_tokPos, S_Symbol($3));} 

decs:   /* empty */                         {$$ = NULL;}
        |  vardec decs                		{$$ = A_DecList($1, $2);}
        |  typedec decs               	    {$$ = A_DecList($1, $2);}
        |  funcdec decs                		{$$ = A_DecList($1, $2);}

vardec:    VAR ID ASSIGN exp           		{$$ = A_VarDec(EM_tokPos, S_Symbol($2), NULL, $4);}
        |  VAR ID COLON ID ASSIGN exp  		{$$ = A_VarDec(EM_tokPos, S_Symbol($2), S_Symbol($4), $6);}

typedec:   TYPE ID EQ ty              		{$$ = A_TypeDec(EM_tokPos, A_NametyList(A_Namety(S_Symbol($2), $4), NULL));}        

funcdec:   FUNCTION ID LPAREN tyfields RPAREN EQ exp   			{$$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), $4, NULL, $7), NULL));}
        |  FUNCTION ID LPAREN tyfields RPAREN COLON ID EQ exp   {$$ = A_FunctionDec(EM_tokPos, A_FundecList(A_Fundec(EM_tokPos, S_Symbol($2), $4, S_Symbol($7), $9), NULL));}

tyfields: /* empty */                       {$$ = NULL;} 
        | tyfields1                         {$$ = $1;}

tyfields1: ID COLON ID                      {$$ = A_FieldList(A_Field(EM_tokPos, S_Symbol($1), S_Symbol($3)), NULL);}
        |  ID COLON ID COMMA tyfields1      {$$ = A_FieldList(A_Field(EM_tokPos, S_Symbol($1), S_Symbol($3)), $5);}



